buildscript {
    ext.kotlin_version = '1.9.0'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // This code is where all the magic happens and fixes the error.
    subprojects {
        // To fix the namespace error caused by external packages.
        // https://stackoverflow.com/a/77738936/7983864
        afterEvaluate { project ->
            if (project.hasProperty('android')) {
                project.android {
                    if (namespace == null) {
                        namespace project.group
                    }
                }
            }

            /// To fix the error caused by gallery_saver_updated not using Java 17
            /// https://stackoverflow.com/a/79175616/7983864
            if (project.plugins.hasPlugin("com.android.application")
                    || project.plugins.hasPlugin("com.android.library")) {

                if (project.name == "gallery_saver_updated") {
                    project.android.compileOptions {
                        sourceCompatibility = JavaVersion.VERSION_17
                        targetCompatibility = JavaVersion.VERSION_17
                    }
                }
            }
        }
    }
    // This code is where all the magic happens and fixes the error.
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
